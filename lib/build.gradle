plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
}

group 'org.jetbrains'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.7.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

tasks.named("test") {
    useJUnitPlatform {
        excludeTags "performance-test"
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Only up-to-date if 'example-input-directories' stays the same.
tasks.register("performanceTest", Test) {
    minHeapSize = "128m"
    maxHeapSize = "2048m"

    useJUnitPlatform {
        includeTags "performance-test"
    }
    testLogging {
        events "passed", "skipped", "failed"
    }

    description = "Runs performance tests"
    group = "verification"
}